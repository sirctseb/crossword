type Matrix<Type> extends Map<Index, Map<Index, Type>>;
type Presence<Type> extends Map<Type, Boolean>;

type Crossword {
    rows: Number
    symmetric: Boolean
    themeEntries: Boolean[]
    clues: Map<Direction, Matrix<String>>
    boxes: Matrix<Box>
    title: String | Null

    read() {
        auth.uid == root.permissions[key()].owner ||
        root.permissions[key()].collaborators[auth.uid]
    }

    create() {
        auth.uid == root.permissions[key()].owner
    }

    delete() {
        auth.uid == root.permissions[key()].owner
    }

    update() {
        auth.uid == root.permissions[key()].owner ||
        root.permissions[key()].collaborators[auth.uid]
    }
}

type Index extends String {
    validate() {
        this.test(/^[0-9]+$/)
    }
}

type Direction extends String {
    validate() {
        this.test(/(across|down)/)
    }
}

type Box {
    blocked: Boolean | Null,
    circled: Boolean | Null,
    shaded: Boolean | Null,
    content: String | Null,
}

path /crosswords is Crossword[] {}

type CrosswordMetadata {
    title: String | Null,
}

type WordlistEntry {
    word: String,
    usedBy: Presence<CrosswordId>,
}

type User {
    crosswords: CrosswordMetadata[],
    wordlist: WordlistEntry[],

    write() {
        key() == auth.uid
    }
    read() {
        key() == auth.uid
    }
}

path /users is User[] {}

type Permissions {
    owner: UserId,
    collaborators: Presence<UserId>,
    global: Boolean | Null,

    create() {
        auth.uid != null &&
        auth.uid == this.owner
    }
    update() {
        auth.uid == this.owner
    }
    delete() {
        auth.uid == this.owner
    }
    validate() {
        this.owner == auth.uid &&
        (
            prior(this.owner) == null ||
            this.owner == prior(this.owner)
        )
    }
}

// would like to be able to ensure this user exists but we don't own that list
type UserId extends String {}

type CrosswordId extends String {
    validate() {
        root.crosswords[key()] != null
    }
}

path /permissions is Map<CrosswordId, Permissions> {}
